{
  description = "Ganbot - Multi-platform bot (Discord & IRC) built in Rust";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    rust-overlay.url = "github:oxalica/rust-overlay";
    flake-utils.url = "github:numtide/flake-utils";
    crane.url = "github:ipetkov/crane";
  };

  outputs = { self, nixpkgs, rust-overlay, flake-utils, crane, ... }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        overlays = [ (import rust-overlay) ];
        pkgs = import nixpkgs {
          inherit system overlays;
        };
        lib = pkgs.lib;

        rustToolchain = pkgs.rust-bin.nightly.latest.default.override {
          extensions = [ "rust-src" "rust-analyzer" ];
        };

        craneLib = (crane.mkLib pkgs).overrideToolchain rustToolchain;

        # Keep Cargo sources plus the assets that `include_str!` / `include_bytes!`
        # pull in at compile time (prompts, fonts).
        src = lib.cleanSourceWith {
          src = ./.;
          filter = path: type:
            (craneLib.filterCargoSources path type)
            || (lib.hasInfix "/prompts/" path)
            || (lib.hasInfix "/fonts/" path)
            || (lib.hasInfix "/templates/" path);
          name = "ganbot-source";
        };

        commonArgs = {
          inherit src;
          strictDeps = true;

          nativeBuildInputs = with pkgs; [ pkg-config ];
          buildInputs = with pkgs; [ openssl libwebp ];

          PKG_CONFIG_PATH = "${pkgs.openssl.dev}/lib/pkgconfig";
        };

        # Build *only* the dependencies. Cached until Cargo.lock changes.
        cargoArtifacts = craneLib.buildDepsOnly commonArgs;

        # Build the workspace, reusing the pre-built dependency artifacts.
        ganbot = craneLib.buildPackage (commonArgs // {
          inherit cargoArtifacts;
          doCheck = false;
        });

        devBuildInputs = with pkgs; [
          pkg-config
          openssl
          libwebp

          rustToolchain
          cargo-watch
          cargo-edit
          cargo-outdated
          cargo-audit
          cargo-machete
          cargo-features-manager
          cargo-flamegraph
          bacon

          eslint
        ];
      in
      {
        packages.default = ganbot;

        # Dev shell keeps mold + clang for fast incremental local builds.
        devShells.default = pkgs.mkShell.override {
          stdenv = pkgs.stdenvAdapters.useMoldLinker pkgs.clangStdenv;
        } {
          buildInputs = devBuildInputs;
          nativeBuildInputs = [ rustToolchain ];

          PKG_CONFIG_PATH = "${pkgs.openssl.dev}/lib/pkgconfig";
          RUST_BACKTRACE = 1;
          RUST_LOG = "ganbot=debug";

          shellHook = ''
            echo "Ganbot Rust development environment"
            echo "Rust version: $(rustc --version)"
            echo ""
            echo "Available commands:"
            echo "  cargo build    - Build the project"
            echo "  cargo run      - Run the bot"
            echo "  cargo watch    - Watch for changes and rebuild"
            echo "  cargo test     - Run tests"
            echo "  cargo check    - Check for compilation errors"
            echo "  bacon          - Run bacon for continuous checking"
            echo "  cargo machete   - Remove old deps"
            echo "  cargo features prune"
            echo "  eslint         - Lint JavaScript files"
            echo ""
          '';
        };

        apps.default = flake-utils.lib.mkApp {
          drv = ganbot;
        };

        # Useful extra checks you can run with `nix flake check`.
        checks = {
          inherit ganbot;

          ganbot-clippy = craneLib.cargoClippy (commonArgs // {
            inherit cargoArtifacts;
            cargoClippyExtraArgs = "--all-targets -- -D warnings";
          });

          ganbot-fmt = craneLib.cargoFmt {
            inherit src;
          };
        };
      });
}
